package rs.ac.bg.etf.pp1;

import java_cup.runtime.*;
import org.apache.log4j.*;
import rs.ac.bg.etf.pp1.ast.*;
import rs.etf.pp1.symboltable.concepts.*;

import java.util.*;
import rs.ac.bg.etf.pp1.test.CompilerError;
import rs.ac.bg.etf.pp1.test.CompilerError.CompilerErrorType;


parser code {:

	Logger log = Logger.getLogger(getClass());
	boolean errorDetected = false;
	private List<CompilerError> synErrors = new ArrayList<>();
	
   
   
    // slede redefinisani metodi za prijavu gresaka radi izmene teksta poruke
     
    public void report_fatal_error(String message, Object info) throws java.lang.Exception {
      done_parsing();
      report_error(message, info);
    }
  
    public void syntax_error(Symbol cur_token) {
        report_error("\nSintaksna greska", cur_token);
    }
  
    public void unrecovered_syntax_error(Symbol cur_token) throws java.lang.Exception {
        report_fatal_error("Fatalna greska, parsiranje se ne moze nastaviti", cur_token);
    }

    public void report_error(String message, Object info) {
    	errorDetected = true;
    	StringBuilder msg = new StringBuilder(message); 
    	if (info instanceof Symbol)
    	{
    		msg.append (" na liniji ").append(((Symbol)info).left);
			CompilerError c= new CompilerError(((Symbol)info).left, msg.append (" na liniji ").append(((Symbol)info).left).toString(), CompilerErrorType.SYNTAX_ERROR);
			synErrors.add(c);
    	}
        log.error(msg.toString());
    }
    
    public List<CompilerError> getErrors()
	{
		return synErrors;
	}


:}


scan with {:
	Symbol s = this.getScanner().next_token();
	if (s != null && s.value != null) 
		log.info(s.toString() + " " + s.value.toString());
	return s;
:}

init with {:
	errorDetected = false;
:}


terminal PROG, BREAK, CLASS, ENUM, ELSE, CONST, IF, DO, WHILE, NEW, PRINT, READ, RETURN, VOID, EXTENDS, CONTINUE, CASE;
terminal PLUS, MINUS, MUL, DIV, MOD, BOOL_EQUAL, BOOL_NOT_EQUAL, GREATER, GREATER_OR_EQUAL, LESS, LESS_OR_EQUAL;
terminal AND, OR, EQUALS, INC, DEC, SEMI, COLON, COMMA, LPAREN, RPAREN, LSQUARE, RSQUARE, LBRACE, RBRACE, CONDITIONAL;
terminal PLUS_EQUALS, MINUS_EQUALS, MUL_EQUALS, DIV_EQUALS, MOD_EQUALS, MODIFOPERATOR;

terminal Boolean BOOL_CONST;
terminal Integer NUM_CONST;
terminal Character CHAR_CONST;
terminal String IDENT;


nonterminal rs.etf.pp1.symboltable.concepts.Obj ProgName, Program, ProgDeclList, ConstDecl, ConstList, ConstIdent;
nonterminal rs.etf.pp1.symboltable.concepts.Obj VarDecl, VarDeclList, VarIdent;
nonterminal rs.etf.pp1.symboltable.concepts.Obj Designator, DesignatorList, MethodTypeName;
nonterminal rs.etf.pp1.symboltable.concepts.Struct Type, Expr, ExprList, Statement, Term, Factor;
nonterminal rs.etf.pp1.symboltable.concepts.Struct Condition, CondTerm, CondFact;

// nonterminal ProgDeclList, ConstDecl, ConstList, ConstIdent;
// nonterminal VarDecl, VarDeclList, VarIdent;
nonterminal ClassDecl, ClassVarDeclList;
nonterminal MethodDeclList, MethodDecl, MethodVarDeclList;
nonterminal FormPars, FormalParamList, FormalParamDecl;
nonterminal StatementList;
nonterminal DesignatorStatement, LSquareBrace;
// nonterminal Term;
// nonterminal Factor;
nonterminal Assignop, Mulop, Addop;
nonterminal Relop, BeginIf, BeginElse, RParenResolution, OrCondition;
nonterminal StatementStartLabel;

precedence left ELSE;

/* Program */

Program ::= (Program) PROG ProgName:p ProgDeclList LBRACE MethodDeclList RBRACE;

ProgName ::= (ProgName) IDENT:progName;

ProgDeclList ::= (ProgConstDeclList) ProgDeclList ConstDecl
				|
				(ProgVarDeclList) ProgDeclList VarDecl
				|
				(NoProgDeclList) /* epsilon */
				;
				
/* END_Program */


/* ConstDecl */

ConstDecl ::= (ConstDecl) CONST Type ConstList SEMI;

ConstList ::= (SingleConstDecl) ConstIdent
			|
			(MultipleConstDecl) ConstList COMMA ConstIdent
			;
				
ConstIdent ::= (BooleanConst) IDENT:booleanConstName EQUALS BOOL_CONST:booleanConstValue
			|
			(IntegerConst) IDENT:integerConstName EQUALS NUM_CONST:integerConstValue
			|
			(CharacterConst) IDENT:characterConstName EQUALS CHAR_CONST:characterConstValue
			;
			
/* END_ConstDecl */


/* VarDecl */

VarDecl ::= (VarDecl) Type:varType VarDeclList SEMI;

VarDeclList ::= (SingleVarDecl) VarIdent
			|
			(MultipleVarDecl) VarDeclList COMMA VarIdent
			;

VarIdent ::= (OneVar) IDENT:varName
			|
			(ArrayVar) IDENT:varName LSQUARE RSQUARE
			|
			error:e {: parser.report_error("Oporavak od greske na liniji: " + eleft, null); :};

/* END_VarDecl */


/* ClassDecl */



/* END_ClassDecl */


/* MethodDeclList */

MethodDeclList ::= (MethodDeclarations) MethodDeclList MethodDecl
				|
				(NoMethodDecl) /* epsilon */
				;
				
MethodDecl ::= (MethodDecl) MethodTypeName LPAREN FormPars RPAREN MethodVarDeclList LBRACE StatementList RBRACE;

MethodTypeName ::= (NonVoidMethodType) Type:retType IDENT:methodName
				|
				(VoidMethodType) VOID IDENT:methodName
				;

MethodVarDeclList ::= (MethodVariables) MethodVarDeclList VarDecl
				|
				(NoMethodVariables) /* epsilon */
				;

/* END_MethodDeclList */


/* FormPars */

FormPars ::= (FormParams) FormalParamList
		  |
		  (NoFormParam) /* epsilon */
		  ;

FormalParamList ::= (FormalParamDecls) FormalParamList COMMA FormalParamDecl
				|
				(SingleFormalParamDecl) FormalParamDecl
				;

FormalParamDecl ::= (OneFormalParam) Type:formalParamType IDENT:varName
				|
				(ArrayFormalParam) Type:formalParamType IDENT:varName LSQUARE RSQUARE
				;

/* END_FormPars */


/* Type */

Type ::= (Type) IDENT:typeName;

/* END_Type */


/* Statement */

Statement ::= (DesignatorStatementInStatement) DesignatorStatement SEMI
			|
			(Read) READ LPAREN Designator RPAREN SEMI
			|
			(PrintWithoutArgs) PRINT LPAREN Expr RPAREN SEMI
			|
			(PrintWithArgs) PRINT LPAREN Expr COMMA NUM_CONST RPAREN SEMI
			|
			(IfStatement) BeginIf LPAREN Condition RParenResolution Statement
			|
			(IfElseStatement) BeginIf LPAREN Condition RParenResolution Statement BeginElse Statement
			|
			(StatementBlock) LBRACE StatementList RBRACE
			;

StatementList ::= (Statements) StatementList StatementStartLabel Statement
				|
				(NoStatements) /* epsilon */
				;
				
StatementStartLabel ::= (StatementStartLabel) /* epsilon */;

/* END_Statement */

/* If, Condition */
BeginIf ::= (BeginIf) IF;
BeginElse ::= (BeginElse) ELSE;
RParenResolution ::= (RParenResolution) RPAREN;

Condition ::= (CondTermList) Condition OrCondition CondTerm
			|
			(SingleCondTerm) CondTerm
			;

CondTerm ::= (CondFactList) CondTerm AND CondFact
			|
			(SingleCondFact) CondFact
			;

CondFact ::= (SingleCondFactExpr) Expr
			|
			(RelopCondFactExpr) Expr Relop Expr
			;

OrCondition ::= (OrCondition)OR;


/* END*/


/* DesignatorStatement */

DesignatorStatement ::= (AssingDesignatorStatement) Designator Assignop Expr
					|
					(IncDesignatorStatement) Designator INC
					| 
					(DecDesignatorStatement) Designator DEC
					;

/* END_DesignatorStatement */

/* Designator */

Designator ::= (Designator) IDENT:name DesignatorList;

DesignatorList ::= (DesignatorListArray) LSquareBrace Expr RSQUARE
				|
				(NoDesignatorList) /* epsilon */
				;
LSquareBrace ::= (LSquareBrace) LSQUARE;		
				

/* END_Designator */

/* Expr */

Expr ::= (NegativeExpr) MINUS ExprList
		|
		(PositiveExpr) ExprList
		;

ExprList ::= (MulitExprList) ExprList Addop Term
			|
			(SingelExprList) Term
			;

/* END_Expr */


/* Term */

Term ::= (FactorList) Term Mulop Factor
		|
		(SingleFactor) Factor
		;

/* END_Term*/


/* Factor */

Factor ::= (FactorNumConst) NUM_CONST
		|
		(FactorCharConst) CHAR_CONST
		|
		(FactorBooleanConst) BOOL_CONST
		|
		(FactorNewArray) NEW Type:arrType LSQUARE Expr RSQUARE
		|
		(FactorExpr) LPAREN Expr RPAREN
		|
		(FactorDesignator) Designator
		;


/* END_Factor*/

/* Operators */

Assignop ::= (EqualsOperation) EQUALS
			|
			error:e {: parser.report_error("Oporavak od greske na liniji: " + eleft, null); :};

Addop ::= (Plus) PLUS
		|
		(Minus) MINUS
		|
		(Modifoperator) MODIFOPERATOR
		;

Mulop ::= (Multiplication) MUL
		|
		(Division) DIV
		|
		(Moduo) MOD
		;
		
Relop ::= (BoolEqual) BOOL_EQUAL
		|
		(BoolNotEqual) BOOL_NOT_EQUAL
		|
		(Greater) GREATER
		| 
		(GreaterOrEqual) GREATER_OR_EQUAL
		|
	 	(Less) LESS
	 	|
	 	(LessOrEqual) LESS_OR_EQUAL
	 	;

/* End_Operators */










